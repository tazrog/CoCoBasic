1 REM//INCOME VS EXPENSE BASIC PROGRAM FOR THE COCO PROGRAMED BY TAZROG//
2 REM//REQUIES COCO SDC COPYRIGHT 2024//
3 REM//V.05//
4 CLS:FORA=0TO8:READA$:POKE1024+A,VAL("&H"+A$):NEXTA:EXEC1024:DATAC6,1,96,BC,1F,2,7E,96,A3
5 CLEAR 500:Q=0:GT=0:DT=0:IT=0:QQ=0
6 GOTO 383
7 GOSUB 145:GOSUB 153:GOSUB 311
8 CLS:T=150
9 PRINT@7,"INCOME VS EXPENSE":IF DA$=""THEN GOTO 11
10 PRINT@234,"DATE ";DA$:PRINT@263,"PRESS N TO UPDATE";
11 A$= INKEY$:IF A$="N" THEN GOTO 15
12 T=T-1:IF T<1 THEN 25
13 IF A$=" "THEN 25
14 PRINT@293,"PRESS SPACE TO START":PRINT@360,"AUTO START IN";INT(T/30):GOTO 11
15 CLS:PRINT@7,"INCOME VS EXPENSE":PRINT@233,"";:PRINT "MONTH =##";MO$;:INPUT M$:IF M$=""THEN M$=MO$
16 IF M$="1"THEN M$ ="01"ELSE IF M$="2"THEN M$="02"ELSE IF M$="3"THEN M$ ="03"ELSE IF M$="6"THEN M$="06"
17 IF M$="7"THEN M$ ="07"ELSE IF M$="8"THEN M$="08"ELSE IF M$="9"THEN M$ = "09" 
18 PRINT@265,"DAY =##";DL$;:INPUT D$:IF D$=""THEN D$=DL$:GOTO 22
19 IF D$="1"THEN D$="01"ELSE IF D$="2"THEN D$="02"ELSE IF D$="3"THEN D$="03"ELSE IF D$="6"THEN D$="06"
20 IF D$="7"THEN D$="07"ELSE IF D$="8"THEN D$="08"ELSE IF D$="9"THEN D$="09" 
21 IF VAL(D$)>31 THEN CLS:PRINT "INVALID DAY, CHOOSE AGAIN":FOR T=T TO 100:NEXT T:CLS:GOTO 18
22 DA$=M$+"/"+D$:CLS:PRINT@10,"DATE ";:PRINT DA$:PRINT"":T=0:INPUT"IS THIS CORRECT, N TO UPDATE";A$
23 IF A$="N"THEN GOTO 15
24 GOSUB 304
25 CLS:PRINT@268,"LOADING":GOTO 515
26 LO=40:CLS:PRINT"       --> DATE ";:PRINT DA$;:PRINT" <--":GT=IT-DT:A=0:A$=""
27 PRINT@LO-4,"***INCOME VS EXPENSE***";:LO=LO+32:PRINT@LO,"1- ENTER INCOME";:LO=LO+32:PRINT@LO,"2- ENTER EXPENSE";:LO=LO+32:PRINT@LO,"3- LIST INCOME";:LO=LO+32
28 PRINT@LO,"4- LIST EXPENSE";:LO=LO+32:PRINT@LO,"5- MONTHLY IVE";:LO=LO+32:PRINT@LO,"6- IVE GRAPH";:LO=LO+32:PRINT@LO,"7- CAT. INCOME";:LO=LO+32:PRINT@LO,"8- CAT. EXPENSE";:LO=LO+32
29 PRINT@LO,"9- SETTINGS":LO=LO+32:PRINT@LO-8,STRING$(32,"-"):PRINT@32*12,"INCOME  >>>";:PRINT@19+32*12,"";:PRINT USING"$$###,###.##";IT
30 PRINT@32*13,"EXPENSE >>>";:PRINT@19+32*13,"";:PRINT USING"$$###,###.##";DT
31 IF GT>0 THEN PRINT@32*14,"+$FLOW  >>>";ELSE PRINT@32*14,"-$FLOW  >>>";
32 PRINT@19+32*14,"";:PRINT USING"$$###,###.##";GT
33 A$=INKEY$
34 IF A$="1"OR A$="I"THEN 47
35 IF A$="2"OR A$="E"THEN 59
36 IF A$="3"THEN 74
37 IF A$="4"THEN 110
38 IF A$="5"OR A$="M"THEN GOTO 162
39 IF A$="6"OR A$="G"THEN 449
40 IF A$="7"THEN CLS:GOTO 535
41 IF A$="8"THEN CLS:GOTO 530
42 IF A$="S"OR A$="9"THEN 238
43 IF A$="H"THEN GOSUB 376
44 IF A$="Q"THEN EXP
45 IF A$="R"THEN 25
46 GOTO 33
47 CLS:PRINT@6,"INCOME TRANSACTION":PRINT@70,"ENTER AMOUNT";:INPUT AM 
48 IF AM=0 GOTO 25
49 GOSUB 203 
50 AM$=STR$(AM):CLS:PRINT@6,"INCOME TRANSACTION":PRINT@165,"TRANSACTION ENTERED":PRINT@225,DA$;:PRINT" ";:PRINT CI$;:PRINT USING"$$##,###.##";AM
51 PRINT"":PRINT"      PRESS [C] TO CHANGE":PRINT"":PRINT"   PRESS [A] TO ADD ANOTHER":INPUT" PRESS ENTER FOR MAIN SCREEN";A$
52 IF A$="C"THEN 47
53 OPEN"D",#1,"IN.DAT",30:FIELD#1,10 AS DD$,10 AS TT$,10 AS CA$
54 IF A$="Q"THEN END
55 LSET DD$=DA$:LSET TT$=CI$:LSET CA$=AM$:PUT#1,1+Q:Q=Q+1:CLOSE#1
56 IT=IT+AM:GT=GT+IT
57 IF A$="A"THEN 47
58 GOTO 25
59 CLS:PRINT@6,"EXPENSE TRANSACTION":PRINT@70,"ENTER AMOUNT";:INPUT IM 
60 IF IM = 0 GOTO 25
61 GOSUB 203
62 IM$=STR$(IM)
63 CLS: PRINT@6,"EXPENSE TRANSACTION":PRINT@165, "TRANSACTION ENTERED":PRINT@225,DA$;:PRINT" ";:PRINT CI$;:PRINT USING "$$##,###.##";IM
64 PRINT"":PRINT"      PRESS [C] TO CHANGE":PRINT"":PRINT"   PRESS [A] TO ADD ANOTHER":INPUT " PRESS ENTER FOR MAIN SCREEN";A$
65 IF A$="C"THEN 59
66 OPEN"D",#1,"OUT.DAT",30:FIELD #1,10 AS PD$,10 AS PT$,10 AS PA$
67 LSET PD$=DA$:LSET PT$=CI$:LSET PA$=IM$
68 PUT#1,1+K:K=K+1:CLOSE#1
69 L=VAL(A$):
70 DT=DT-(IM*-1):GT=GT+DT
72 IF A$="A"THEN 59
73 GOTO 25
74 OPEN"D",#1,"IN.DAT",30:IT=0:K$="INCOME"
75 LO=31:CLS:INPUT "MONTH## OR FIND";JJ$:IF JJ$ =""THEN JJ$=LEFT$(DA$,2)
76 CLS:IF LOF(1) < 1 THEN PRINT"NO DATA TO SHOW":CLOSE #1: GOTO 25
77 PRINT"#    DATE   INCOME      AMOUNT"
78 FIELD#1,10 AS DD$,10 AS TT$,10 AS CA$
79 FOR E=1 TO LOF(1):GET#1,E:IF VAL(CA$)<1 THEN 89
80 IF LEN(JJ$)>2 THEN IF VAL(JJ$)=VAL(CA$) THEN GOTO 85 ELSE IF JJ$=LEFT$(TT$,LEN(JJ$)) THEN K$="?-"+JJ$:GOTO 85
81 IF JJ$="13"THEN GOTO 84
82 GG$=LEFT$(DD$,2):IF GG$=JJ$ THEN 85
83 GOTO 89
84 REM IF VAL(CA$)<.01 THEN GOTO 89
85 IT=IT + VAL(CA$):PRINT@LO,E;
86 PRINT@LO+6,DD$;:PRINT@LO+14,TT$;:PRINT@LO+21,"";:PRINT USING"$$##,###.##";VAL(CA$)
87 LO=LO+32
88 IF LO>448THEN PRINT@480,"PRESS ENTER KEY";:INPUT L:CLS:PRINT "#    DATE     INCOME      AMOUNT":LO=31
89 NEXT E 
90 PRINT@448,K$:PRINT@462,"";:PRINT USING"total$$###,###.##";IT:CLOSE#1
91 PRINT@480,"[M]-MENU [R]-REDO ##-DELETE";:INPUT A$:IF A$="R" THEN 74
92 IF A$=" "THEN 100
93 IF A$="Q"THEN END
94 IF A$="M"THEN 25
95 IF A$="H"THEN GOSUB 376
96 IF A$="S"OR A$="9"THEN 238
97 IF A$="6"OR A$="G"THEN 449
98 IF A$="5"OR A$="M"THEN 162
99 IF VAL(A$)>0 THEN L= VAL(A$):GOTO 101
100 GOSUB 145:GOTO 25
101 INPUT"DELETE Y/N";A$:IF A$ ="Y" OR A$ ="Y"THEN AM$="":GOTO 103
102 GOTO 108
103 OPEN"D",#1,"IN.DAT",30
104 FIELD#1,10 AS DD$,10 AS TT$,10 AS CA$
105 IF AM$=""THEN TJ$=DA$:DA$="":CI$=""
106 LSET DD$=DA$:LSET TT$=CI$:LSET CA$=AM$
107 IF AM$=""THEN DA$=TJ$:PUT#1,L:CLOSE#1
108 CLEAR 500:GOSUB 145
109 GOTO 74
110 OPEN"D",#1,"OUT.DAT",30:DT=0
111 LO=31: CLS:INPUT"MONTH## OR FIND";JJ$:IF JJ$=""THEN JJ$=LEFT$(DA$,2)
112 CLS:IF LOF(1)<1 THEN PRINT"NO DATA TO SHOW":CLOSE#1:GOTO 25
113 PRINT"#    DATE   EXPENSE      AMOUNT"
114 FIELD#1,10 AS PD$,10 AS PT$,10 AS PA$:K$="EXPENSE"
115 FOR E=1 TO LOF(1):GET#1,E:IF VAL(PA$)<1 THEN GOTO 124
116 IF LEN(JJ$)>2 THEN IF VAL(JJ$)=VAL(PA$) THEN GOTO 121 ELSE IF JJ$=LEFT$(PT$,LEN(JJ$)) THEN K$="?-"+JJ$:GOTO 121
117 IF JJ$="13"THEN 120
118 GG$=LEFT$(PD$,2):IF GG$=JJ$ GOTO 121
119 GOTO 124
120 REM IF VAL(PA$)<1 THEN GOTO 124
121 DT=DT+VAL(PA$) 
122 PRINT@LO,E;:PRINT@LO+6,PD$;:PRINT@LO+14,PT$;:PRINT@LO+21,"";:PRINT USING"$$##,###.##";VAL(PA$)
123 LO=LO+32:IF LO>448 THEN PRINT@480,"PRESS ENTER KEY";:INPUT L:CLS:PRINT"#    DATE    EXPENSE     AMOUNT":LO=31
124 NEXT E 
125 PRINT@448,K$:PRINT @462,"";:PRINT USING "total$$###,###.##";DT:CLOSE#1
126 PRINT@480,"[M]-MENU [R]-REDO ##-DELETE";:INPUT A$:IF A$="R" THEN 110
127 IF A$=" "THEN GOTO 134
128 IF A$="Q"THEN END
129 IF A$="M"THEN 25
130 IF A$="S"OR A$="9"THEN 238
131 IF A$="6"OR A$="G"THEN 449
132 IF A$="5"OR A$="M"THEN 162
133 L=VAL(A$):IF L>0 THEN 135
134 GOSUB 153:GOTO 25
135 INPUT"DELETE Y/N";A$:IF A$ ="Y" OR A$ ="Y"THEN IM$="":GOTO 137
136 GOTO 143
137 OPEN"D",#1,"OUT.DAT",30
138 IF IM$=""THEN TJ$=DA$:DA$="":CI$=""
139 FIELD #1,10 AS PD$,10 AS PT$,10 AS PA$
140 LSET PD$=DA$:LSET PT$=CI$:LSET PA$=IM$
141 PUT#1,L:IF IM$=""THEN DA$=TJ$
142 CLOSE #1
143 GOSUB 153
144 GOTO 110
145 OPEN"D",#1,"IN.DAT",30:GT=0:IT=0
146 IF LOF(1) <1 THEN CLOSE #1:RETURN
147 FIELD#1,10 AS DD$,10 AS TT$,10 AS CA$:Q=LOF(1)
148 FOR E=1 TO Q
149 GET#1,E
150 IT=IT+VAL(CA$)
151 NEXT E
152 CLOSE#1:RETURN
153 OPEN"D",#1,"OUT.DAT",30:GT=0:DT=0
154 IF LOF(1) <1 THEN CLOSE #1:RETURN
155 FIELD #1,10 AS DD$,10 AS TD$,10 AS CD$
156 K=LOF(1)
157 FOR E=1 TO K
158 GET#1,E 
159 DT=DT+VAL(CD$)
160 NEXT E:CLOSE#1
161 RETURN
162 CLS:PRINT@267,"CALCULATING";
163 FOR X=1 TO 12:MI(X)=0:MD(X)=0:NEXT X
164 OPEN"D",#1,"IN.DAT",30
165 IF LOF(1)<1 THEN CLOSE #1:GOTO 174
166 FIELD#1,10 AS MD$,10 AS MC$,10 AS MZ$
167 G=LOF(1)
168 FOR E=1 TO G
169 GET#1,E
170 MT=VAL(LEFT$(MD$,2))
171 GOSUB 196
172 NEXT E
173 CLOSE #1
174 OPEN"D",#1,"OUT.DAT",30:MU=0:MG=0:MP=0
175 IF LOF(1)<1 THEN CLOSE #1:A$="R":GOTO 25
176 FIELD #1,10 AS ID$,10 AS IC$,10 AS IZ$
177 G=LOF(1)
178 FOR E=1 TO G
179 PRINT@302,"";:PRINT USING"###";(E/G)*100;:PRINT"%"
180 GET#1,E:IQ=VAL(LEFT$(ID$,2))
181 GOSUB 199
182 NEXT E 
183 CLOSE#1
184 CLS(0):LO=32
185 PRINT"m";:PRINT@4,"income";:PRINT@14,"expense";:PRINT@27,"diff";
186 FOR X= 1 TO 12
187 PRINT@LO,RIGHT$(STR$(X),1);:PRINT@LO+1,"";:PRINT USING"#######.##";MI(X);:MU=MU+MI(X)
188 PRINT@LO+12,"";:PRINT USING"#######.##";MD(X);:MG=MG+MD(X):PRINT@LO+23,"";:PRINT USING"######.##";MI(X)-MD(X);:MP=MP+(MI(X)-MD(X))
189 LO=LO+32:NEXT X
190 PRINT@LO+13,"totals";:LO=LO+32:PRINT@LO,"":PRINT @ LO+2,"";:PRINT USING"######.##";MU
191 PRINT@LO+13,"";:PRINT USING"######.##";MG:PRINT@LO+23,"";:PRINT USING"######.##";MP;
192 A$=INKEY$
193 IF A$=" "THEN GOSUB 145:GOSUB 153:GOSUB 311:GOTO 25
194 IF A$="G"THEN 449
195 GOTO 192
196 FOR X=1 TO 12
197 IF X=MT THEN MI(MT)=MI(MT)+VAL(MZ$)
198 NEXT X:RETURN
199 FOR X=1 TO 12
200 IF X=IQ THEN MD(IQ)=MD(IQ)+VAL(IZ$)
201 NEXT X
202 RETURN
203 REM//CATEGORIES//
204 CLS:PRINT"          CATEGORIES"
205 OPEN"D",#1,"CAT.DAT",10:QQ=0:LO=32
206 IF LOF(1)<1THEN QQ=99:CLOSE#1:GOTO 226
207 FIELD #1,10 AS CT$
208 G=LOF(1)
209 FOR E=1 TO G
210 GET#1,E 
211 PRINT@LO,E;:PRINT"-";
212 PRINT CT$;
213 CG$(E)=CT$
214 LO=LO+32/2
215 NEXT E
216 PRINT"":PRINT@15*32,"";
217 CLOSE#1
218 IF A=6 THEN INPUT"99 TO ADD OR ## TO EDIT";QQ
219 IF A=6 AND QQ=99 THEN 226 
220 IF A=6 AND QQ<1 THEN A$="R":GOTO 25
221 IF A=6 THEN 225
222 INPUT"ENTER CAT.# OR 99 TO ADD";L:IF L=99 THEN QQ=99:GOTO 226
223 CI$=CG$(L):A$=""
224 A$="R":RETURN
225 PRINT"EDIT CATEGORIES"
226 OPEN"D",#1,"CAT.DAT",10
227 FIELD#1,10 AS CA$
228 IF B>24 THEN 237
229 IF QQ=99 THEN B=LOF(1)+1:GOTO 231
230 IF QQ<99 AND QQ>0 THEN B=QQ
231 IF B>28 THEN CLOSE#1:PRINT "MAX CAT": FOR X=1 TO 1500:NEXT X: GOTO 25 
232 INPUT"CAT NAME";CD$
233 LSET CA$=CD$
234 PUT#1,B
235 CLOSE#1
236 IF A<6 THEN GOTO 203
237 A$="R":GOTO 25
238 REM //SETTING SCREEN//
239 CLS:A$=""
240 PRINT"SETTINGS":PRINT"":PRINT"1- ADD/DELETE CATEGORIES":PRINT"2- CLEAR INCOME DATA":PRINT"3- CLEAR EXPENSE DATA":PRINT"3- CLEAR CATEGORIES DATA":PRINT"5- CHANGE DATE"
246 PRINT"6- BACKUP":PRINT"7- CLOSE OUT YEAR":PRINT"[SPACE]- MAIN MENU":PRINT"H- HELP"
250 A$=INKEY$
251 IF A$="1"THEN A=6:GOSUB 203:GOTO 238
252 IF A$="2"THEN PRINT"CLEARED INCOME DATA":INPUT"ARE YOU SURE <Y/N>?";L$: IF L$ ="Y"THEN GOTO 261 ELSE GOTO 25
253 IF A$="3"THEN PRINT"CLEARED EXEPNSE DATA":INPUT"ARE YOU SURE <Y/N>?";L$: IF L$ ="Y"THEN GOTO 262 ELSE GOTO 25
254 IF A$="4"THEN PRINT"CLEARED CATEGORIES":INPUT"ARE YOU SURE <Y/N>?";L$: IF L$ ="Y"THEN GOTO 263 ELSE GOTO 25
255 IF A$="5"THEN PRINT"NEW DATE":GOTO 15
256 IF A$="6"THEN GOTO 265
257 IF A$="7"THEN GOTO 278
258 IF A$=" "THEN GOTO 25
259 IF A$="H"GOSUB 376: GOTO 238
260 GOTO 250
261 KILL"IN/DAT":IT=0:GOTO 25
262 KILL"OUT/DAT":DT=0:GOTO 25
263 KILL"CAT/DAT":GOTO 25
264 GOTO 25
265 REM//BACKUP// 
266 CLS:PRINT"":A$=""
267 CLEAR 500
268 PRINT"THIS REQUIRES THE COCO SDC ":PRINT"ENTER [C] TO CANCEL": INPUT"ANY OTHER KEY TO EXEC";A$:IF A$="C" THEN 238
270 CLS:DRIVE1,"IVEBU.DSK",NEW: PRINT "FORMATING DRIVE":DSKINI1
271 PRINT"DRIVE FORMATTED":PRINT"CONDUCTING BACKUP"
273 BACKUP 0 TO 1
274 PRINT"BACKUP COMPLETE":PRINT"BACKUP DISK NAMED IVEBU.DSK"
276 FOR X=1 TO 1500:NEXT X
277 GOTO 25
278 REM //YEAR CLOSE OUT//
279 CLEAR 500:CLS
280 PRINT"THIS WILL CLOSEOUT THE YEAR,":PRINT"AND ARCHIVE THE DATA ON DISK,":PRINT"AND CREATE A NEW YEAR.":PRINT"THIS REQUIRES THE COCO SDC."
284 PRINT"ARE YOU SURE?":INPUT"TYPE YES AND ENTER TO CONT.";B$: IF B$="YES"THEN GOTO 285 ELSE GOTO 25
285 CLS:PRINT"YEAR CLOSE OUT AND NEW YEAR DISK."
286 INPUT"WHAT YEAR(##) IS THIS FOR?";A$ 
287 IF LEN(A$)<2 OR LEN(A$)>2 THEN PRINT"USE ONLY 2 DIGITS FOR YEAR.":GOTO 286
288 PRINT"ENTER [C] TO CANCEL":INPUT"ANY OTHER KEY TO EXECUTE.";B$: IF B$="C"THEN 238
289 CLS:DRIVE1,"IVE"+A$+"-C.DSK",NEW:PRINT"FORMATING DRIVE":DSKINI1
290 PRINT"DRIVE FORMATTED.":PRINT"TRANSFERING DATA TO DISK."
292 BACKUP 0 TO 1
293 PRINT"CLOSEOUT COMPLETE.":PRINT"ARCHIVE DISK NAMED IVE"+A$+".DSK"
295 CLEAR 500
296 INPUT"ENTER NEW YEAR ## ";A$
297 IF LEN(A$)<2 OR LEN(A$)>2 THEN PRINT"USE ONLY 2 DIGITS FOR YEAR.": FOR X =1 TO 500:NEXT X: GOTO 296
298 PRINT"CREATING NEW YEAR."
299 KILL"IN/DAT":KILL"OUT/DAT":KILL"DATE/DAT":DA$="01/01":GOSUB 304
300 DRIVE1,"IVE"+A$+".DSK",NEW:PRINT"FORMATING DRIVE.":DSKINI1:BACKUP 0 TO 1
301 PRINT"CLOSEOUT COMPLETE."
302 FOR X=1 TO 1500:NEXT X
303 EXP
304 OPEN"D",#1,"DATE.DAT",5
305 FIELD#1,5 AS DJ$
306 B=LOF(1)
307 LSET DJ$=DA$
308 PUT#1,1
309 CLOSE#1
310 RETURN
311 OPEN"D",#1,"DATE.DAT",5
312 B=LOF(1)
313 IF B<1 THEN CLOSE #1:CLS:PRINT"CREATE NEW YEAR DISK":GOTO 295
314 FIELD#1,5 AS DJ$
315 GET#1,1
316 DA$=DJ$
317 MO$=LEFT$(DJ$,2):DL$=RIGHT$(DJ$,2)
319 CLOSE#1
320 RETURN
321 OPEN"D",#1,"CAT.DAT",10:A$="":S=0:A$=""
322 IF LOF(1) <1 THEN CLOSE#1:A$="R":GOTO 25
323 FIELD#1,10 AS CT$
324 G=LOF(1)
325 FOR E=1 TO G
326 GET#1,E 
327 CG$(E)=CT$
328 NEXT E
329 CLOSE#1
330 FOR X=1 TO G 
331 OT(X)=0
332 NEXT X
333 IP=0
334 OPEN"D",#1,"OUT.DAT",30
335 IF LOF(1)<1THEN CLOSE #1:A$="R":GOTO 25
336 FIELD#1,10 AS ID$,10 AS IC$,10 AS IZ$
337 P=LOF(1)
338 FOR E=1 TO P
339 PRINT@302,"";:PRINT USING"###";(E/P)*100;:PRINT"%"
340 GET#1,E:IF WW$=""THEN GOTO 343
341 IF LEFT$(ID$,2)=WW$ THEN 343
342 GOTO 345
343 IL$=IC$
344 IP=VAL(IZ$)
345 FOR W=1 TO G:IF WW$=""GOTO 348
346 IF LEFT$(ID$,2)=WW$ THEN 348
347 GOTO 349
348 IF IL$=CG$(W) THEN OT(W)=OT(W)+IP
349 NEXT W,E
351 CLOSE#1
352 CLS(0):LO=32
353 PRINT@0,"category";:PRINT @15,K$;:PRINT @ 24,"expense";
354 FOR X = 1 TO G
355 IF OT(X)<1 THEN GOTO 360
356 PRINT@LO,CG$(X);:PRINT STRING$(6," ");:
357 PRINT@LO+18,"";:PRINT USING"$$###,###.##";OT(X)
358 LO=LO+32
359 IF LO>448 THEN PRINT@480,"PRESS ENTER TO CONT.";:INPUT L:CLS(0):PRINT@0,"category";:PRINT@15,K$;:PRINT@24,"expense";:LO=32
360 NEXT X
361 PRINT@480,"";:INPUT"M-MENU OR MONTH ##        ";A$:WW$=""
362 IF A$="1"THEN A$="01"ELSE IF A$="2"THEN A$="02"ELSE IF A$="3"THEN A$="03"ELSE IF A$="6"THEN A$="06"
363 IF A$="7"THEN A$ ="07"ELSE IF A$="8"THEN A$="08"ELSE IF A$="9"THEN A$="09" 
364 IF A$="M"THEN GOTO 25
365 IF A$="G"THEN 449
366 IF A$="S"THEN 238
367 IF A$=""GOTO 375
368 IF A$="H"GOSUB 376
369 IF A$="R"GOTO 25
370 WW$=A$
371 IF A$="01"THEN K$="jan"ELSE IF A$="02"THEN K$="feb"ELSE IF A$="03"THEN K$="mar"ELSE IF A$="04"THEN K$="apr"ELSE IF A$="05"THEN K$="may"ELSE IF A$="06"THEN K$="jun"ELSE IF A$="07"THEN K$="jul"ELSE IF A$="08"THEN K$="aug"
372 IF A$="09"THEN K$="sep"ELSE IF A$="10"THEN K$="oct"ELSE IF A$="11"THEN K$="nov"ELSE IF A$="12"THEN K$="dec"
373 IF VAL(WW$)<13 THEN CLS:PRINT@267,"CALCULATING";:GOTO 321
374 IF WW$="13"THEN CLS(0):GOTO 540
375 WW$="":A$="R":GOTO 25
376 REM//HELP SCREEN//
377 CLS:A$="":PRINT"           HELP":PRINT"SHORTCUTS":PRINT"[H] KEY-HELP SCREEN":PRINT"[S] KEY-SETTINGS":PRINT"[M] KEY-MONTHLY #-LIST SCREENS":PRINT"[G] KEY-GRAPHS ON MONTHLY"
378 PRINT"[Q] KEY-QUIT":PRINT"[SPACE] KEY-MAIN MENU":PRINT"[ENTER] KEY- MAIN MENU"
379 A$=INKEY$
380 IF A$=" "THEN A$="R":RETURN
381 IF A$="Q"THEN END
382 GOTO 379
383 REM //SPLASH SCREEN//
384 CLS:C=143:A$="":PRINT@332,"FOR THE":PRINT@358,"TANDY COLOR COMPUTER":PRINT@396,"COCO SDC":PRINT@72,"INCOME VS EXPENSE":PRINT@171,"CREATED BY"
385 PRINT@205,"TAZROG":PRINT@238,"2024":PRINT@458,"PRESS SPACE":T=0
386 FOR ZZ=0TO1STEP0:FORX=0TO15:POKEX+1024,C:POKEX+1040,C:POKE1535-X,C:POKE1519-X,C:POKE1055+(32*X),C:POKE1472-(32*X),C:GOSUB387:NEXT:GOSUB387:NEXT
387 C=C+16:IF C>255 THEN C=143
388 A$=INKEY$
389 T=T+1
390 IF T >70 THEN 7
391 IF A$=" "GOTO 7
392 RETURN
393 REM //INCOME REPORTS//
394 OPEN"D",#1,"CAT.DAT",10:A$="":S=0
395 IF LOF(1)<1 THEN CLOSE#1:A$="R":GOTO 25
396 FIELD#1,10 AS CT$
397 G=LOF(1)
398 FOR E=1 TO G
399 GET#1,E 
400 CG$(E)=CT$
401 NEXT E
402 CLOSE#1
403 FOR X=1 TO G 
404 OT(X)=0
405 NEXT X
406 IP=0
407 OPEN "D",#1,"IN.DAT",30
408 IF LOF(1) <1 THEN CLOSE #1:A$="R":GOTO 25
409 FIELD #1,10 AS ID$,10 AS IC$,10 AS IZ$
410 P=LOF(1)
411 FOR E=1 TO P
412 PRINT @ 302,"";:PRINT USING "###";(E/P)*100;:PRINT"%"
413 GET #1,E :IF WW$="" THEN GOTO 416
414 IF LEFT$(ID$,2)=WW$ THEN 416
415 GOTO 418
416 IL$=IC$
417 IP= VAL(IZ$)
418 FOR W=1 TO G:IF WW$="" GOTO 421
419 IF LEFT$(ID$,2)=WW$ THEN 421
420 GOTO 422
421 IF IL$=CG$(W) THEN OT(W)=OT(W)+IP
422 NEXT W,E
424 CLOSE #1
425 CLS(0):LO=32
426 PRINT@0,"category";:PRINT @15,K$;:PRINT @ 24,"income";
427 FOR X=1 TO G
428 IF OT(X)<1 THEN GOTO 433
429 PRINT@LO,CG$(X);:PRINT STRING$(6," ");:
430 PRINT@ LO+18,"";:PRINT USING "$$###,###.##";OT(X)
431 LO=LO+32
432 IF LO>448 THEN PRINT@480,"PRESS ENTER TO CONT.";:INPUT L:CLS(0):PRINT@0,"category";:PRINT@15,K$;:PRINT@24,"income";:LO=32
433 NEXT X
434 PRINT@480,"";:INPUT"M-MENU OR MONTH ##        ";A$:WW$=""
435 IF A$="1"THEN A$ ="01"ELSE IF A$= "2"THEN A$="02"ELSE IF A$="3"THEN A$ ="03"ELSE IF A$="6"THEN A$="06"
436 IF A$="7"THEN A$="07"ELSE IF A$="8"THEN A$="08"ELSE IF A$="9"THEN A$="09" 
437 IF A$="M"THEN CLS(0):GOTO 25
438 IF A$="G"THEN 449
439 IF A$="S"THEN 238
440 IF A$=""THEN CLS(0):GOTO 448
441 IF A$="H"GOSUB 376
442 IF A$="R"GOTO 25
443 WW$=A$
444 IF A$="01"THEN K$="jan"ELSE IF A$="02"THEN K$="feb"ELSE IF A$="03"THEN K$="mar"ELSE IF A$="04"THEN K$="apr"ELSE IF A$="05"THEN K$="may"ELSE IF A$="06"THEN K$="jun"ELSE IF A$="07"THEN K$="jul"ELSE IF A$="08"THEN K$="aug"
445 IF A$="09"THEN K$="sep"ELSE IF A$="10"THEN K$="oct"ELSE IF A$="11"THEN K$="nov"ELSE IF A$="12"THEN K$="dec"
446 IF VAL(WW$)<13 THEN CLS:PRINT@267,"CALCULATING";:GOTO 393
447 IF WW$="13"THEN CLS(0):GOTO 544
448 WW$="":A$="R":GOTO 25
449 REM//CALCULATIONS FOR INCOME/EXPENSE REPORTS//
450 CLS:HT=0:HM=0:HH=0:A$=""
451 FOR X=1TO12:MI(X)=0:MD(X)=0:NEXT X
452 PRINT@267,"CALCULATING";
453 FOR X=1 TO 12:MI(X)=0:MD(X)=0
454 OPEN"D",#1,"IN.DAT",30
455 IF LOF(1) <1 THEN CLOSE #1:GOTO 174
456 FIELD #1,10 AS MD$,10 AS MC$,10 AS MZ$
457 G=LOF(1)
458 FOR E=1 TO G
459 GET#1,E
460 MT=VAL(LEFT$(MD$,2))
461 GOSUB 505
462 NEXT E
463 CLOSE#1
464 OPEN"D",#1,"OUT.DAT",30:MU=0:MG=0:MP=0
465 IF LOF(1)<1 THEN CLOSE #1:A$="R":RETURN
466 FIELD #1,10 AS ID$,10 AS IC$,10 AS IZ$
467 G=LOF(1)
468 FOR E=1 TO G
469 GET#1,E 
470 IQ= VAL(LEFT$(ID$,2))
471 PRINT@302,"";:PRINT USING"###";(E/G)*100;:PRINT"%"
472 GOSUB 510
473 NEXT E 
474 CLOSE#1
475 IF HT<HH THEN HT=HH
476 IF HT<>INT(HT) THEN HT=INT(HT)+1
477 HM=HT/2:IF HM <>INT(HM) THEN HM=INT(HM)+1
478 CLS(0):Y$=STRING$(1,126):PRINT @7,"income";:SET(13,0,1):SET(13,1,1):SET(26,0,1):SET(26,1,1):PRINT@16,"vs";:PRINT@21,"expense";:SET(41,0,4):SET(41,1,4):SET(56,0,4):SET(56,1,4)
479 PRINT@420,Y$;:PRINT@433,Y$;:PRINT@447,Y$;
480 PRINT@451,STR$(0):PRINT@448+17-LEN(STR$(HM))/2,HM:PRINT@480-LEN(STR$(HT)),STR$(HT);
481 FOR X= 1 TO 12:IF X=1 THEN X$="jan"ELSE IF X=2 THEN X$="feb"ELSE IF X=3 THEN X$="mar"ELSE IF X=4 THEN X$="apr"ELSE IF X=5 THEN X$="may"ELSE IF X=6 THEN X$="jun"ELSE IF X=7 THEN X$="jul"ELSE IF X=8 THEN X$="aug"
482 IF X=9 THEN X$="sep"ELSE IF X=10 THEN X$="oct"ELSE IF X=11 THEN X$="nov"ELSE IF X=12 THEN X$="dec"
483 PRINT@32*X,X$;
484 IF MI(X)>MD(X) THEN BB=MI(X):C=1
485 IF MD(X)>=MI(X) THEN BB= MD(X):C=4
486 IF BB <>INT(BB)THEN BB=INT(BB)+1
487 FOR G=8TO (BB/(HT/55)+8)
488 IF BB < 1 THEN 490
489 SET (G,2*X,C)
490 NEXT G 
491 IF MI(X)<MD(X) THEN BD=MI(X):C=1: 
492 IF MD(X)<=MI(X) THEN BD=MD(X):C=4
493 IF BD <>INT(BD) THEN BD=INT(BD)+1
494 FOR H=8 TO (BD/(HT/55)+8)
495 IF BD<1 GOTO 497
496 SET (H,2*X,C)
497 NEXT H
498 NEXT X
499 A$=INKEY$
500 IF A$=" "THEN GOTO 25
501 IF A$="M"THEN GOTO 25
502 IF A$="S"THEN GOTO 238
503 IF A$="H"GOSUB 376: GOTO 25
504 GOTO 499
505 FOR X=1 TO 12
506 IF X=MT THEN MI(MT)=MI(MT)+VAL(MZ$)
507 IF MI(X)>HT THEN HT=MI(X)
508 NEXT X
509 RETURN
510 FOR X=1 TO 12
511 IF X= IQ THEN MD(IQ)=MD(IQ)+VAL(IZ$)
512 IF MD(X)>HH THEN HH=MD(X)
513 NEXT X 
514 RETURN
515 REM//MEMORY MANAGAMENT//
516 CLEAR 500:CLS:PRINT@268,"LOADING"
517 DIM MI(25):DIM MD(25):DIM CG$(25):DIM OT(25):HT=0:HH=0
518 GOSUB 311:GOSUB 145:GOSUB 153
519 GOTO 26
520 CLEAR 500:CLS:PRINT@268,"LOADING"
521 DIM MI(25):DIM MD(25):DIM CG$(25):DIM OT(25):HT=0:HH=0
522 GOSUB 311:GOSUB 145
523 CLS:PRINT@267,"CALCULATING":WW$=LEFT$(DA$,2):K$="MONTH"
524 GOTO 321
525 CLEAR 500:CLS:PRINT@268,"LOADING"
526 DIM MI(25):DIM MD(25):DIM CG$(25):DIM OT(25):HT=0:HH=0
527 GOSUB 311:GOSUB 153
528 CLS:PRINT@267,"CALCULATING":K$="MONTH"
529 GOTO 393
530 CLEAR 500:CLS:PRINT@268,"LOADING"
531 DIM MI(25):DIM MD(25):DIM CG$(25):DIM OT(25)
532 GOSUB 311:GOSUB 145
533 CLS:PRINT@267,"CALCULATING":IF VAL(WW$)<13 THEN A$=MO$ ELSE K$="year":GOTO 321
534 GOTO 362
535 CLEAR 500:CLS:PRINT@268,"LOADING"
536 DIM MI(25):DIM MD(25):DIM CG$(25):DIM OT(25)
537 GOSUB 311:GOSUB 153
538 CLS:PRINT@267,"CALCULATING":IF VAL(WW$)<13 THEN A$=MO$ ELSE K$="year":GOTO 393
539 GOTO 435
540 CLEAR 500:CLS:PRINT@268,"LOADING"
541 DIM MI(25):DIM MD(25):DIM CG$(25):DIM OT(25)
542 GOSUB 311:GOSUB 145
543 CLS:PRINT@266,"CALCULATING":K$="year": GOTO 321
544 CLEAR 500:CLS:PRINT@268,"LOADING"
545 DIM MI(25):DIM MD(25):DIM CG$(25):DIM OT(25)
546 GOSUB 311:GOSUB 145
547 CLS:PRINT@267,"CALCULATING":K$="year": GOTO 393
548 END
549 
 
