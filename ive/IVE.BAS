1 REM INCOME VS EXPENSE BASIC PROGRAM FOR THE COCO PROGRAMED BY R. SCHARMEN
2 REM REQUIES COCO SDC TO BACKUP COPYRIGHT 2024
5 CLEAR 500:Q=0:GT=0:DT=0:IT=0:QQ=0
7 GOTO 12500
10 GOSUB 5000:GOSUB 6000:GOSUB 10500
40 CLS:T=150
45 PRINT @ 7, "INCOME VS EXPENSE":IF DA$="" THEN GOTO 51
50 PRINT @ 234,"DATE ";DA$:PRINT @ 263,"PRESS N TO UPDATE";
51 A$= INKEY$:IF A$="N" THEN GOTO 55
52 T=T-1:IF T<1 THEN 140
53 IF A$= " " THEN 140
54 PRINT @ 293,"PRESS SPACE TO START": PRINT @ 360,"AUTO START IN";INT(T/30):GOTO 51
55 CLS: PRINT @ 7, "INCOME VS EXPENSE":PRINT @233,"";:PRINT "MONTH =##";MO$;:INPUT M$:IF M$="" THEN M$=MO$
60 IF M$="1" THEN M$ ="01" ELSE IF M$= "2" THEN M$="02" ELSE IF M$="3" THEN M$ = "03" ELSE IF M$="6" THEN M$="06"
65 IF M$="7" THEN M$ ="07" ELSE IF M$= "8" THEN M$="08" ELSE IF M$="9" THEN M$ = "09" 
70 PRINT @ 265,"DAY =## ";DL$;
75 INPUT D$:IF D$="" THEN D$=DL$:GOTO 90
80 IF D$="1" THEN D$ ="01" ELSE IF D$= "2" THEN D$="02" ELSE IF D$="3" THEN D$ = "03" ELSE IF D$="6" THEN D$="06"
85 IF D$="7" THEN D$ ="07" ELSE IF D$= "8" THEN D$="08" ELSE IF D$="9" THEN D$ = "09" 
86 IF VAL(D$)>31 THEN CLS: PRINT "INVALID DAY, CHOOSE AGAIN":FOR T=T TO 100: NEXT T:CLS: GOTO 70
90 DA$=M$+"/"+D$
100 CLS: PRINT @ 10,"DATE ";:PRINT DA$:PRINT "":T=0
110 INPUT "IS THIS CORRECT, N TO UPDATE";A$
120 IF A$= "N" THEN GOTO 55
135 GOSUB 10000
140 CLS: PRINT @ 269, "LOADING":GOTO 15000

141 REM MAIN MENU
145 LO=40:CLS:PRINT "       --> DATE ";:PRINT DA$;:PRINT" <--":GT=IT-DT:A=0:A$=""
150 PRINT @LO-4,"***INCOME VS EXPENSE***";:LO=LO+32
160 PRINT @LO,"1- ENTER INCOME";:LO=LO+32
170 PRINT @LO,"2- ENTER EXPENSE";:LO=LO+32
180 PRINT @LO,"3- LIST INCOME";:LO=LO+32
190 PRINT @LO,"4- LIST EXPENSE";:LO=LO+32
200 PRINT @LO,"5- MONTHLY IVE";:LO=LO+32
202 PRINT @LO,"6- IVE GRAPH";:LO=LO+32
205 PRINT @LO,"7- CAT. INCOME";:LO=LO+32
207 PRINT @LO,"8- CAT. EXPENSE";:LO=LO+32
210 PRINT @LO,"S- SETTINGS":LO=LO+32
220 PRINT @LO-8,STRING$(32,"-")
230 PRINT @32*12,"INCOME  >>>";:PRINT @ 19+32*12, "";:PRINT USING "$$###,###.##";IT
240 PRINT @32*13,"EXPENSE >>>";:PRINT @ 19+32*13, "";:PRINT USING"$$###,###.##";DT
250 IF GT>0 THEN PRINT @32*14,"+$FLOW  >>>"; ELSE PRINT @32*14, "-$FLOW  >>>";
255 PRINT @ 19+32*14, "";:PRINT USING "$$###,###.##";GT
260 A$=INKEY$
270 IF A$="1" THEN 1000
280 IF A$="2" THEN 2000
290 IF A$="3" THEN 3000
300 IF A$="4" THEN 4000
310 IF A$="5" THEN GOTO 7000
315 IF A$="6" THEN 14000
325 IF A$="7" THEN CLS:GOTO 15400
328 IF A$="8" THEN CLS:GOTO 15300
330 IF A$="S" OR A$="S" THEN 9000
335 IF A$="H" OR A$="H" THEN GOSUB 12000
340 IF A$="Q" OR A$="Q" THEN END
350 IF A$="R" THEN 140
360 GOTO 260

1000 REM LIST INCOME 
1001 CLS
1010 PRINT @ 6,"INCOME TRANSACTION":PRINT @ 70,"ENTER AMOUNT";:INPUT AM 
1015 IF AM = 0 GOTO 140
1020 GOSUB 8000 
1025 AM$=STR$(AM)
1030 CLS: PRINT @ 6,"INCOME TRANSACTION":PRINT @ 165, "TRANSACTION ENTERED":PRINT @ 225,DA$;:PRINT" ";:PRINT CI$;:PRINT USING "$$##,###.##";AM
1040 PRINT"":PRINT "      PRESS [C] TO CHANGE"
1041 PRINT "":INPUT "   PRESS [A] TO ADD ANOTHER";A$
1042 IF A$="C" THEN 1000
1500 OPEN "D",#1,"IN.DAT",30
1510 FIELD #1,10 AS DD$,10 AS TT$,10 AS CA$
1512 IF A$="Q" THEN END
1550 LSET DD$=DA$
1560 LSET TT$=CI$
1570 LSET CA$=AM$
1575 PUT #1,1+Q
1580 Q=Q+1
1590 CLOSE #1
1615 IT=IT+AM
1617 GT=GT +IT
1618 IF A$="A" THEN 1000
1620 GOTO 140

2000 REM LIST EXPENSES
2001 CLS
2010 PRINT @ 6,"EXPENSE TRANSACTION":PRINT @70,"ENTER AMOUNT";:INPUT IM 
2015 IF IM = 0 GOTO 140
2020 GOSUB 8000
2025 IM$=STR$(IM)
2030 CLS: PRINT @ 6,"EXPENSE TRANSACTION":PRINT @ 165, "TRANSACTION ENTERED":PRINT @ 225,DA$;:PRINT" ";:PRINT CI$;:PRINT USING "$$##,###.##";IM
1040 PRINT "":PRINT "      PRESS [C] TO CHANGE"
1041 PRINT "":INPUT "   PRESS [A] TO ADD ANOTHER";A$
1042 IF A$="C" THEN 2000
2500 OPEN "D",#1,"OUT.DAT",30
2510 FIELD #1,10 AS PD$,10 AS PT$,10 AS PA$
2550 LSET PD$=DA$
2560 LSET PT$=CI$
2570 LSET PA$=IM$
2575 PUT #1,1+K
2580 K=K+1
2590 CLOSE #1
2600 L=VAL(A$)
2615 DT=DT-(IM*-1)
2617 GT=GT +DT
2618 IF A$="A" THEN 2000
2620 GOTO 140

3000 REM ENTER INCOME
3001 OPEN "D",#1,"IN.DAT",30:IT=0
3002 LO =31: CLS: INPUT "MONTH TO DISPLAY ## 13 FOR ALL";JJ$:IF JJ$ = "" THEN JJ$ = LEFT$(DA$,2)
3005 CLS:IF LOF(1) < 1 THEN PRINT "NO DATA TO SHOW":CLOSE #1: GOTO 140
3006 PRINT "#    DATE   INCOME      AMOUNT"
3010 FIELD #1,10 AS DD$,10 AS TT$,10 AS CA$
3020 FOR E=1 TO LOF(1)
3030 GET #1,E 
3031 IF JJ$="13" THEN GOTO 3039
3035 GG$=LEFT$(DD$,2):IF GG$=JJ$ THEN 3040
3037 GOTO 3050
3039 IF VAL(CA$)<.01 THEN GOTO 3050
3040 IT = IT + VAL(CA$): PRINT @ LO, E;
3041 PRINT @ LO+6, DD$;
3042 PRINT @ LO +14, TT$;
3043 PRINT @ LO +21, "";
3045 PRINT USING "$$##,###.##";VAL(CA$)
3047 LO =LO +32
3048 IF LO> 448 THEN PRINT @ 480, "PRESS ENTER KEY";:INPUT L:CLS:PRINT "#    DATE     INCOME      AMOUNT":LO=31
3050 NEXT E 
3055 PRINT @ 448,"";:PRINT USING "           total  $$###,###.##";IT
3060 CLOSE #1
3070 PRINT @ 480, "[M]-MENU OR ##-DELETE";:INPUT A$
3080 IF A$=" " THEN 3095
3085 IF A$="Q" THEN END
3088 IF A$="M" THEN 140
3087 IF A$="H" THEN GOSUB 12000
3090 IF VAL(A$)>0 THEN L= VAL(A$): GOTO 3100
3095 GOSUB 5000:GOTO 140
3100 INPUT "DELETE Y/N";A$:IF A$ ="Y" OR A$ ="Y" THEN AM$="":GOTO 3120
3110 GOTO 3195
3120 OPEN "D",#1,"IN.DAT",30
3130 FIELD #1,10 AS DD$,10 AS TT$,10 AS CA$
3140 IF AM$="" THEN TJ$=DA$:DA$="":CI$=""
3150 LSET DD$=DA$
3160 LSET TT$=CI$
3170 LSET CA$=AM$
3175 IF AM$="" THEN DA$=TJ$
3180 PUT #1,L
3190 CLOSE #1
3195 CLEAR 500: GOSUB 5000
3200 GOTO 3000

4000 REM ENTER EXPENSES
4001 OPEN "D",#1,"OUT.DAT",30:DT=0
4002 LO =31: CLS: INPUT "MONTH TO DISPLAY ## 13 FOR ALL";JJ$:IF JJ$ = "" THEN JJ$ = LEFT$(DA$,2)
4005 CLS:IF LOF(1) < 1 THEN PRINT "NO DATA TO SHOW":CLOSE #1: GOTO 140
4006 PRINT "#    DATE   EXPENSE      AMOUNT"
4010 FIELD #1,10 AS PD$,10 AS PT$,10 AS PA$
4020 FOR E=1 TO LOF(1)
4030 GET #1,E
4031 IF JJ$="13" THEN 4034
4032 GG$=LEFT$(PD$,2):IF GG$=JJ$ GOTO 4035
4033 GOTO 4050
4034 IF VAL(PA$)<1 THEN GOTO 4050
4035 DT = DT + VAL(PA$) 
4040 PRINT @ LO,E;
4042 PRINT @ LO +6,PD$;
4043 PRINT @ LO +14,PT$;
4044 PRINT @ LO +21,"";
4045 PRINT USING "$$##,###.##";VAL(PA$)
4047 LO =LO +32
4048 IF LO> 448 THEN PRINT @ 480, "PRESS ENTER KEY";:INPUT L:CLS:PRINT "#    DATE    EXPENSE     AMOUNT":LO=31
4050 NEXT E 
4055 PRINT @ 448,"";:PRINT USING "           total  $$###,###.##";DT
4060 CLOSE #1
4070 PRINT @ 480, "[M]-MENU OR ##-DELETE";:INPUT A$
4075 IF A$=" " THEN GOTO 4095
4078 IF A$="Q" THEN END
4079 IF A$="M" THEN 140
4080 L=VAL(A$) 
4090 IF L > 0 THEN 4100
4095 GOSUB 6000:GOTO 140
4100 INPUT "DELETE Y/N";A$:IF A$ ="Y" OR A$ ="Y" THEN IM$="":GOTO 4120
4110 GOTO 4195
4120 OPEN "D",#1,"OUT.DAT",30
4125 IF IM$="" THEN TJ$=DA$:DA$="":CI$=""
4130 FIELD #1,10 AS PD$,10 AS PT$,10 AS PA$
4150 LSET PD$=DA$
4160 LSET PT$=CI$
4170 LSET PA$=IM$
4180 PUT #1,L
4185 IF IM$="" THEN DA$=TJ$
4190 CLOSE #1
4195 GOSUB 6000
4200 GOTO 4000

5000 REM INCOME DATA
5001 OPEN "D",#1,"IN.DAT",30:GT=0:IT=0
5005 IF LOF(1) <1 THEN CLOSE #1:RETURN
5007 FIELD #1,10 AS DD$,10 AS TT$,10 AS CA$
5010 Q=LOF(1)
5020 FOR E=1 TO Q
5030 GET #1,E
5040 IT=IT+VAL(CA$)
5050 NEXT E
5060 CLOSE #1
5070 RETURN

6000 REM EXPENSE DATA
6001 OPEN "D",#1,"OUT.DAT",30:GT=0:DT=0
6005 IF LOF(1) <1 THEN CLOSE #1:RETURN
6007 FIELD #1,10 AS DD$,10 AS TD$,10 AS CD$
6010 K=LOF(1)
6020 FOR E=1 TO K
6030 GET #1,E 
6040 DT=DT+VAL(CD$)
6050 NEXT E
6060 CLOSE #1
6070 RETURN

7000 REM MONTHLY PRINT CHART
7001 CLS: PRINT @ 267,"CALCULATING";
7005 FOR X=1 TO 12:MI(X)=0:MD(X)=0:NEXT X
7100 OPEN "D",#1,"IN.DAT",30
7105 IF LOF(1) <1 THEN CLOSE #1:GOTO 7200
7107 FIELD #1,10 AS MD$,10 AS MC$,10 AS MZ$
7110 G=LOF(1)
7120 FOR E=1 TO G
7130 GET #1,E
7140 MT= VAL(LEFT$(MD$,2))
7150 GOSUB 7500
7160 NEXT E
7170 CLOSE #1
7200 OPEN "D",#1,"OUT.DAT",30:MU=0:MG=0:MP=0
7205 IF LOF(1) <1 THEN CLOSE #1:A$="R":GOTO 140
7207 FIELD #1,10 AS ID$,10 AS IC$,10 AS IZ$
7210 G=LOF(1)
7220 FOR E=1 TO G
7225 PRINT @ 302,"";:PRINT USING "###";(E/G)*100;:PRINT"%"
7230 GET #1,E 
7240 IQ= VAL(LEFT$(ID$,2))
7250 GOSUB 7550
7260 NEXT E 
7270 CLOSE #1
7300 CLS(0):LO=32
7301 PRINT "m";:PRINT @ 4,"income";:PRINT @14,"expense";:PRINT @27,"diff";
7305 FOR X= 1 TO 12
7310 PRINT @ LO,RIGHT$(STR$(X),1);
7320 PRINT @ LO+1,"";:PRINT USING "#######.##";MI(X);:MU=MU+MI(X)
7330 PRINT @ LO+12,"";:PRINT USING "#######.##";MD(X);:MG=MG+MD(X)
7340 PRINT @ LO+23,"";:PRINT USING "######.##";MI(X)-MD(X);:MP=MP+(MI(X)-MD(X))
7345 LO=LO+32
7350 NEXT X
7355 PRINT @ LO+13,"totals";:LO=LO+32
7360 PRINT @ LO,"":PRINT @ LO+2,"";:PRINT USING "######.##";MU
7370 PRINT @ LO+13,"";:PRINT USING "######.##";MG
7380 PRINT @ LO+23,"";:PRINT USING "######.##";MP;
7430 A$=INKEY$
7435 IF A$=" " THEN GOSUB 5000:GOSUB 6000:GOSUB 10500:GOTO 140
7436 IF A$="G" THEN 14000
7440 GOTO 7430
7500 FOR X = 1 TO 12
7510 IF X = MT THEN MI(MT)=MI(MT)+VAL(MZ$)
7530 NEXT X
7540 RETURN
7550 FOR X = 1 TO 12
7560 IF X = IQ THEN MD(IQ)=MD(IQ)+VAL(IZ$)
7570 NEXT X
7800 RETURN

8000 REM CATEGORIES
8001 CLS:PRINT "          CATEGORIES"
8005 OPEN "D",#1,"CAT.DAT",10:QQ=0:LO=64
8010 IF LOF(1) <1 THEN QQ=99:CLOSE#1:GOTO 8530
8020 FIELD #1,10 AS CT$
8030 G=LOF(1)
8040 FOR E=1 TO G
8050 GET #1,E 
8070 PRINT @ LO,E;:PRINT "-";
8071 PRINT CT$;
8075 CG$(E)=CT$
8076 LO=LO+32/2
8080 NEXT E
8081 PRINT "":PRINT""
8085 CLOSE #1
8090 IF A=6 THEN INPUT "99 TO ADD OR ## TO EDIT";QQ
8100 IF A=6 AND QQ=99 THEN 8530 
8105 IF A=6 AND QQ<1 THEN A$="R":GOTO 140
8106 IF A=6 THEN 8500
8107 INPUT "ENTER CAT.# OR 99 TO ADD";L:IF L=99 THEN QQ=99:GOTO 8530
8110 CI$=CG$(L):A$=""
8120 A$="R":RETURN
8500 PRINT "EDIT CATEGORIES"
8530 OPEN "D",#1,"CAT.DAT",10
8540 FIELD #1,10 AS CA$
8550 IF B>24 THEN 8630
8555 IF QQ=99 THEN B=LOF(1)+1:GOTO 8570
8560 IF QQ<99 AND QQ>0 THEN B=QQ
8570 IF B>25 THEN CLOSE #1:PRINT "MAX CAT": FOR X=1 TO 1500:NEXT X: GOTO 140 
8575 INPUT "CAT NAME";CD$
8580 LSET CA$=CD$
8600 PUT #1,B
8610 CLOSE #1
8620 IF A<6 THEN GOTO 8000
8630 A$="R":GOTO 140

9000 REM SETTING SCREEN
9001 CLS
9010 PRINT "SETTINGS":PRINT "" 
9020 PRINT "1- ADD/DELETE CATEGORIES"
9025 PRINT "2- CLEAR INCOME DATA"
9030 PRINT "3- CLEAR EXPENSE DATA"
9040 PRINT "3- CLEAR CATEGORIES DATA"
9050 PRINT "5- CHANGE DATE"
9051 PRINT "6- BACKUP"
9052 PRINT "[SPACE]- MAIN MENU"
9053 PRINT "H- HELP"
9055 A$=INKEY$
9058 IF A$="1" THEN A=6:GOSUB 8000:GOTO 9000
9060 IF A$="2" THEN PRINT "CLEARED INCOME DATA":INPUT "ARE YOU SURE <Y/N>?";L$: IF L$ = "Y" THEN GOTO 9200 ELSE GOTO 140
9070 IF A$="3" THEN PRINT "CLEARED EXEPNSE DATA":INPUT "ARE YOU SURE <Y/N>?";L$: IF L$ = "Y" THEN GOTO 9210 ELSE GOTO 140
9080 IF A$="4" THEN PRINT "CLEARED CATEGORIES":INPUT "ARE YOU SURE <Y/N>?";L$: IF L$ = "Y" THEN GOTO 9220 ELSE GOTO 140
9090 IF A$="5" THEN PRINT "NEW DATE": GOTO 55
9095 IF A$="6" THEN GOTO 9500
9098 IF A$=" " THEN GOTO 140
9099 IF A$="H" GOSUB 12000: GOTO 9000
9100 GOTO 9055
9200 KILL "IN/DAT":IT=0:GOTO 140
9210 KILL "OUT/DAT":DT=0:GOTO 140
9220 KILL "CAT/DAT":GOTO 140
9230 GOTO 140

9500 REM BACKUP 
9501 CLS:PRINT ""
9505 PRINT "THIS REQUIIRES COCO SDC "
9514 PRINT "HIT ENTER TO BACKUP": INPUT"ANY OTHER KEY TO CANCEL";A$:IF A$="N" THEN 9000
9520 CLS: DRIVE1, "IVEBU.DSK",NEW: PRINT "FORMATING DRIVE":DSKINI1
9535 PRINT "DRIVE FORMATTED"
9538 PRINT "CONDUCTING BACKUP"
9540 BACKUP 0 TO 1
9580 PRINT "BACKUP COMPLETE"
9585 PRINT "BACKUP DISK NAMED IVEBU.DSK"
9590 FOR X=1 TO 1500:NEXT X
9600 GOTO 140

10000 OPEN "D",#1,"DATE.DAT",5
10010 FIELD #1,5 AS DJ$
10020 B=LOF(1)
10030 LSET DJ$=DA$
10040 PUT #1,1
10060 CLOSE #1
10070 RETURN
10500 OPEN "D",#1,"DATE.DAT",5
10510 B=LOF(1)
10520 IF B<1 THEN CLOSE #1:RETURN
10530 FIELD #1,5 AS DJ$
10540 GET #1,1
10545 DA$=DJ$
10560 MO$=LEFT$(DJ$,2)
10565 DL$=RIGHT$(DJ$,2)
10570 CLOSE #1
10580 RETURN

11000 OPEN "D",#1,"CAT.DAT",10:A$="":S=0
11010 IF LOF(1) <1 THEN CLOSE#1: A$="R":GOTO 140
11020 FIELD #1,10 AS CT$
11030 G=LOF(1)
11040 FOR E=1 TO G
11050 GET #1,E 
11070 CG$(E)=CT$
11080 NEXT E
11090 CLOSE #1
11100 FOR X=1 TO G 
11110 OT(X)=0
11120 NEXT X
11130 IP=0
11300 OPEN "D",#1,"OUT.DAT",30
11310 IF LOF(1) <1 THEN CLOSE #1:A$="R":GOTO 140
11320 FIELD #1,10 AS ID$,10 AS IC$,10 AS IZ$
11330 P=LOF(1)
11340 FOR E=1 TO P
11345 PRINT @ 302,"";:PRINT USING "###";(E/P)*100;:PRINT "%"
11350 GET #1,E :IF WW$="" THEN GOTO 11355
11352 IF LEFT$(ID$,2)=WW$ THEN 11355
11353 GOTO 11365
11355 IL$=IC$
11360 IP= VAL(IZ$)
11365 FOR W = 1 TO G:IF WW$= "" GOTO 11370
11367 IF LEFT$(ID$,2)=WW$ THEN 11370
11368 GOTO 11375
11370 IF IL$=CG$(W) THEN OT(W)=OT(W)+IP
11375 NEXT W
11390 NEXT E 
11395 CLOSE #1
11400 CLS(0):LO=32
11405 PRINT @0,"category";:PRINT @15,K$;:PRINT @ 24,"expense";
11410 FOR X = 1 TO G
11415 IF OT(X)<1 THEN GOTO 11440
11420 PRINT @LO,CG$(X);:PRINT STRING$(6," ");:
11430 PRINT @ LO+18,"";:PRINT USING "$$###,###.##";OT(X)
11435 LO=LO+32
11437 IF LO>448 THEN PRINT @ 480,"PRESS ENTER TO CONT.";:INPUT L:CLS(0):PRINT @0,"category";:PRINT @15,K$;:PRINT @ 24,"expense";:LO=32
11440 NEXT X
11450 PRINT@ 480,"";:INPUT "M-MENU OR MONTH ##        ";A$:WW$=""
11455 IF A$="1" THEN A$ ="01" ELSE IF A$= "2" THEN A$="02" ELSE IF A$="3" THEN A$ = "03" ELSE IF A$="6" THEN A$="06"
11457 IF A$="7" THEN A$ ="07" ELSE IF A$= "8" THEN A$="08" ELSE IF A$="9" THEN A$ = "09" 
11460 IF A$="M" THEN GOTO 140
11465 IF A$="G" THEN 14000
11467 IF A$="S" THEN 9000
11470 IF A$="" GOTO 11500
11475 IF A$="H" GOSUB 12000
11478 IF A$="R" GOTO 140
11479 WW$=A$
11480 IF A$="01" THEN K$="jan" ELSE IF A$="02" THEN K$="feb" ELSE IF A$="03" THEN K$="mar" ELSE IF A$="04" THEN K$="apr"ELSE IF A$="05" THEN K$="may" ELSE IF A$="06" THEN K$="jun"ELSE IF A$="07" THEN K$="jul"ELSE IF A$="08" THEN K$="aug"
11481 IF A$="09" THEN K$="sep"ELSE IF A$="10" THEN K$="oct" ELSE IF A$="11" THEN K$="nov"ELSE IF A$="12" THEN K$="dec"
11485 IF VAL(WW$)<13 THEN CLS:PRINT @ 267,"CALCULATING";:GOTO 11000
11490 IF WW$="13" THEN CLS(0):GOTO 15500
11500 WW$="":A$="R":GOTO 140

12000 REM HELP SCREEN
12001 CLS
12010 PRINT "           HELP"
12020 PRINT "SHORTCUTS"
12030 PRINT "[H] KEY-HELP SCREEN"
12040 PRINT "[S] KEY-SETTINGS"
12050 PRINT "[M] KEY-MONTHLY #-LIST SCREENS"
12055 PRINT "[G] KEY-GRAPHS ON MONTHLY"
12060 PRINT "[Q] KEY-QUIT"
12070 PRINT "[SPACE] KEY-MAIN MENU"
12080 PRINT "[ENTER] KEY- MAIN MENU"
12100 A$=INKEY$
12110 IF A$=" " THEN A$="R":RETURN
12111 IF A$="Q" THEN END
12120 GOTO 12100

12500 REM SPLASH SCREEN
12501 CLS:C=143
12502 PRINT @ 332, "FOR THE"
12505 PRINT @ 358,"TANDY COLOR COMPUTER"
12507 PRINT @ 396,"COCO SDC"
12510 PRINT @ 72, "INCOME VS EXPENSE"
12520 PRINT @ 171, "CREATED BY"
12530 PRINT @ 202, "R. SCHARMEN"
12535 PRINT @ 237, "2024"
12540 PRINT @ 458, "PRESS SPACE":T=0
12575 FOR ZZ=0TO1STEP0:FORX=0TO15:POKEX+1024,C:POKEX+1040,C:POKE1535-X,C:POKE1519-X,C:POKE1055+(32*X),C:POKE1472-(32*X),C:GOSUB12580:NEXT:GOSUB12580:NEXT
12580 C=C+16:IF C>255 THEN C=143
12590 A$=INKEY$
12591 T=T+1
12592 IF T > 100 THEN 10
12600 IF A$=" " GOTO 10
12610 RETURN

13000 REM INCOME REPORTS
13001 OPEN "D",#1,"CAT.DAT",10:A$="":S=0
13010 IF LOF(1) <1 THEN CLOSE#1: A$="R":GOTO 140
13020 FIELD #1,10 AS CT$
13030 G=LOF(1)
13040 FOR E=1 TO G
13050 GET #1,E 
13070 CG$(E)=CT$
13080 NEXT E
13090 CLOSE #1
13100 FOR X=1 TO G 
13110 OT(X)=0
13120 NEXT X
13130 IP=0
13300 OPEN "D",#1,"IN.DAT",30
13310 IF LOF(1) <1 THEN CLOSE #1:A$="R":GOTO 140
13320 FIELD #1,10 AS ID$,10 AS IC$,10 AS IZ$
13330 P=LOF(1)
13340 FOR E=1 TO P
13345 PRINT @ 302,"";:PRINT USING "###";(E/P)*100;:PRINT"%"
13350 GET #1,E :IF WW$="" THEN GOTO 13355
13352 IF LEFT$(ID$,2)=WW$ THEN 13355
13353 GOTO 13365
13355 IL$=IC$
13360 IP= VAL(IZ$)
13365 FOR W = 1 TO G:IF WW$= "" GOTO 13370
13367 IF LEFT$(ID$,2)=WW$ THEN 13370
13368 GOTO 13375
13370 IF IL$=CG$(W) THEN OT(W)=OT(W)+IP
13375 NEXT W
13390 NEXT E 
13395 CLOSE #1
13400 CLS(0):LO=32
13405 PRINT @0,"category";:PRINT @15,K$;:PRINT @ 24,"income";
13410 FOR X = 1 TO G
13415 IF OT(X)<1 THEN GOTO 13440
13420 PRINT @LO,CG$(X);:PRINT STRING$(6," ");:
13430 PRINT @ LO+18,"";:PRINT USING "$$###,###.##";OT(X)
13435 LO=LO+32
13437 IF LO>448 THEN PRINT @ 480,"PRESS ENTER TO CONT.";:INPUT L:CLS(0):PRINT @0,"category";:PRINT @15,K$;:PRINT @ 24,"income";:LO=32
13440 NEXT X
13450 PRINT@ 480,"";:INPUT "M-MENU OR MONTH ##        ";A$:WW$=""
13455 IF A$="1" THEN A$ ="01" ELSE IF A$= "2" THEN A$="02" ELSE IF A$="3" THEN A$ = "03" ELSE IF A$="6" THEN A$="06"
13457 IF A$="7" THEN A$ ="07" ELSE IF A$= "8" THEN A$="08" ELSE IF A$="9" THEN A$ = "09" 
13460 IF A$="M" THEN CLS(0):GOTO 140
13465 IF A$="G" THEN 14000
13467 IF A$="S" THEN 9000
13470 IF A$="" THEN CLS(0):GOTO 13500
13475 IF A$="H" GOSUB 12000
13478 IF A$="R" GOTO 140
13479 WW$=A$
13480 IF A$="01" THEN K$="jan" ELSE IF A$="02" THEN K$="feb" ELSE IF A$="03" THEN K$="mar" ELSE IF A$="04" THEN K$="apr"ELSE IF A$="05" THEN K$="may" ELSE IF A$="06" THEN K$="jun"ELSE IF A$="07" THEN K$="jul"ELSE IF A$="08" THEN K$="aug"
13481 IF A$="09" THEN K$="sep"ELSE IF A$="10" THEN K$="oct" ELSE IF A$="11" THEN K$="nov"ELSE IF A$="12" THEN K$="dec"
13485 IF VAL(WW$)<13 THEN CLS:PRINT @ 267,"CALCULATING";:GOTO 13000
13490 IF WW$="13" THEN CLS(0):GOTO 15600
13500 WW$="":A$="R":GOTO 140

14000 REM CALCULATIONS FOR INCOME/EXPENSE REPORTS
14001 CLS:HT=0:HM=0:HH=0
14002 FOR X=1TO12:MI(X)=0:MD(X)=0:NEXT X
14005 PRINT @267,"CALCULATING";
14010 FOR X=1 TO 12:MI(X)=0:MD(X)=0
14020 OPEN "D",#1,"IN.DAT",30
14030 IF LOF(1) <1 THEN CLOSE #1:GOTO 7200
14040 FIELD #1,10 AS MD$,10 AS MC$,10 AS MZ$
14050 G=LOF(1)
14060 FOR E=1 TO G
14070 GET #1,E
14080 MT= VAL(LEFT$(MD$,2))
14090 GOSUB 14500
14100 NEXT E
14110 CLOSE #1
14120 OPEN "D",#1,"OUT.DAT",30:MU=0:MG=0:MP=0
14130 IF LOF(1) <1 THEN CLOSE #1:A$="R":RETURN
14140 FIELD #1,10 AS ID$,10 AS IC$,10 AS IZ$
14150 G=LOF(1)
14160 FOR E=1 TO G
14170 GET #1,E 
14180 IQ= VAL(LEFT$(ID$,2))
14185 PRINT @ 302,"";:PRINT USING "###";(E/G)*100;:PRINT "%"
14190 GOSUB 14550
14200 NEXT E 
14210 CLOSE #1
14220 IF HT < HH THEN HT=HH
14225 IF HT <>INT(HT) THEN HT=INT(HT)+1
14226 HM = HT/2:IF HM <>INT(HM) THEN HM =INT(HM)+1
14227 CLS(0):Y$=STRING$(1,126):PRINT @ 7,"income";:SET(13,0,1):SET(13,1,1):SET(26,0,1):SET(26,1,1):PRINT @16, "vs";:PRINT @ 21, "expense";:SET(41,0,4):SET(41,1,4):SET(56,0,4):SET(56,1,4)
14228 PRINT @420, Y$;:PRINT @433,Y$;:PRINT @ 447,Y$;
14229 PRINT @451, STR$(0):PRINT @448+17-LEN(STR$(HM))/2,HM:PRINT @ 480-LEN(STR$(HT)),STR$(HT);
14230 FOR X= 1 TO 12:IF X=1 THEN X$="jan" ELSE IF X=2 THEN X$="feb" ELSE IF X=3 THEN X$="mar" ELSE IF X=4 THEN X$="apr"ELSE IF X=5 THEN X$="may" ELSE IF X=6 THEN X$="jun"ELSE IF X=7 THEN X$="jul"ELSE IF X=8 THEN X$="aug"
14231 IF X=9 THEN X$="sep"ELSE IF X=10 THEN X$="oct"ELSE IF X=11 THEN X$="nov"ELSE IF X=12 THEN X$="dec"
14234 PRINT @ 32*X,X$;
14235 IF MI(X)>MD(X) THEN BB=MI(X):C=1
14236 IF MD(X)>=MI(X) THEN BB= MD(X):C=4
14237 IF BB <>INT(BB)THEN BB=INT(BB)+1
14250 FOR G=8TO (BB/(HT/55)+8)
14255 IF BB < 1 THEN 14270
14260 SET (G,2*X,C)
14270 NEXT G 
14275 IF MI(X)<MD(X) THEN BD=MI(X):C=1: 
14277 IF MD(X)<=MI(X) THEN BD=MD(X):C=4
14280 IF BD <>INT(BD) THEN BD=INT(BD)+1
14290 FOR H = 8 TO (BD/(HT/55)+8)
14295 IF BD<1 GOTO 14310
14300 SET (H,2*X,C)
14310 NEXT H
14320 NEXT X
14330 A$=INKEY$
14340 IF A$=" " THEN GOTO 140
14341 IF A$="M" THEN GOTO 140
14342 IF A$="S" THEN GOTO 9000
14343 IF A$="H" GOSUB 12000: GOTO 140
14350 GOTO 14330
14500 FOR X = 1 TO 12
14510 IF X = MT THEN MI(MT)=MI(MT)+VAL(MZ$)
14515 IF MI(X)>HT THEN HT=MI(X)
14530 NEXT X
14540 RETURN
14550 FOR X = 1 TO 12
14560 IF X = IQ THEN MD(IQ)=MD(IQ)+VAL(IZ$)
14565 IF MD(X)>HH THEN HH=MD(X)
14570 NEXT X 
14800 RETURN

15000 REM MEMORY MANAGAMENT
15001 CLEAR 500:CLS:PRINT @ 269, "LOADING"
15010 DIM MI(25):DIM MD(25):DIM CG$(25):DIM OT(25):HT=0:HH=0
15020 GOSUB 10500:GOSUB 5000:GOSUB 6000
15050 GOTO 145
15100 CLEAR 500:CLS:PRINT @ 269, "LOADING"
15110 DIM MI(25):DIM MD(25):DIM CG$(25):DIM OT(25):HT=0:HH=0
15120 GOSUB 10500:GOSUB 5000
15125 CLS: PRINT @ 267,"CALCULATING":WW$=LEFT$(DA$,2):K$="MONTH"
15150 GOTO 11000
15200 CLEAR 500:CLS:PRINT @ 269, "LOADING"
15210 DIM MI(25):DIM MD(25):DIM CG$(25):DIM OT(25):HT=0:HH=0
15220 GOSUB 10500:GOSUB 6000
15225 CLS: PRINT @ 267,"CALCULATING":K$="MONTH"
15250 GOTO 13000
15300 CLEAR 500:CLS:PRINT @ 269, "LOADING"
15310 DIM MI(25):DIM MD(25):DIM CG$(25):DIM OT(25)
15320 GOSUB 10500:GOSUB 5000
15325 CLS: PRINT @ 267,"CALCULATING":IF VAL(WW$)<13 THEN A$=MO$ ELSE K$="year": GOTO 11000
15350 GOTO 11455
15400 CLEAR 500:CLS:PRINT @ 269, "LOADING"
15410 DIM MI(25):DIM MD(25):DIM CG$(25):DIM OT(25)
15420 GOSUB 10500:GOSUB 6000
15425 CLS: PRINT @ 267,"CALCULATING":IF VAL(WW$)<13 THEN A$=MO$ ELSE K$="year": GOTO 13000
15450 GOTO 13455
15500 CLEAR 500:CLS:PRINT @ 269, "LOADING"
15510 DIM MI(25):DIM MD(25):DIM CG$(25):DIM OT(25)
15520 GOSUB 10500:GOSUB 5000
15525 CLS: PRINT @ 266,"CALCULATING":K$="year": GOTO 11000
15600 CLEAR 500:CLS:PRINT @ 269, "LOADING"
15610 DIM MI(25):DIM MD(25):DIM CG$(25):DIM OT(25)
15620 GOSUB 10500:GOSUB 5000
15625 CLS: PRINT @ 267,"CALCULATING":K$="year": GOTO 13000


16000 END