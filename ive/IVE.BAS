 
5 CLEAR 500:Q=0:GT=0:DT=0:IT=0:QQ=0:GOSUB 5000
10 GOSUB 6000
20 DIM MI(12):DIM MD(12):DIM CG$(12):DIM IL$(12):DIM OT(12)
30 GOSUB 10500
40 CLS
50 PRINT "MONTH -## ";MO$;
51 INPUT M$:IF M$="" THEN M$=MO$
52 IF LEN(M$)<2 THEN PRINT "ENTER MONTH ##":GOTO 50
53 IF LEN(M$)>2 THEN PRINT "ENTER MONTH ##":GOTO 50
55 PRINT "DAY =## ";DL$;
56 INPUT D$:IF D$="" THEN D$=DL$
57 IF LEN(D$)>2 THEN PRINT "ENTER DAY ##":GOTO 55
58 IF LEN(D$)<2 THEN PRINT "ENTER DAY ##":GOTO 55
60 PRINT"YEAR -## ";YE$;
61 INPUT Y$
65 IF Y$="" THEN Y$=YE$
70 IF LEN(Y$)<2 THEN PRINT "ENTER YEAR ##":GOTO 60
80 IF LEN(Y$)>2 THEN PRINT "ENTER YEAR ##":GOTO 60
90 DA$=M$+"/"+D$+"/"+Y$
100 CLS: PRINT "TODAY ";:PRINT DA$:PRINT ""
110 INPUT "IS THIS CORRECT";A$
120 IF A$= "Y" THEN GOTO 135
130 GOTO 20
135 GOSUB 10000
140 CLS:PRINT "        TODAY ";:PRINT DA$:GT=IT-DT:A=0:A$=""
150 PRINT "       income vs expense":PRINT ""
160 PRINT "       1- ENTER INCOME"
170 PRINT "       2- ENTER EXPENSE"
180 PRINT "       3- LIST INCOME"
190 PRINT "       4- LIST EXPENSE"
200 PRINT "       5- MONTHLY NUMBERS"
205 PRINT "       6- CAT SPENDING"
210 PRINT "       7- CATEGORIES"
220 PRINT "*******************************"
230 PRINT USING "INCOME TOTAL";:PRINT @ 21+32*11, "";:PRINT USING "$$#####.##";IT
240 PRINT USING "DEBT TOTAL";:PRINT @ 21+32*12, "";:PRINT USING"$$#####.##";DT
250 PRINT USING "grand total";:PRINT @ 21+32*13, "";:PRINT USING "$$#####.##";GT:PRINT ""
260 A$=INKEY$
270 IF A$="1" THEN 1000
280 IF A$="2" THEN 2000
290 IF A$="3" THEN 3000
300 IF A$="4" THEN 4000
310 IF A$="5" THEN 7000
320 IF A$="7" THEN A=6:GOSUB 8000
325 IF A$ ="6" GOSUB 11000
330 IF A$="S" THEN 9000
340 IF A$="Q" THEN END
350 IF A$="R" THEN 140
360 GOTO 260

1000 CLS
1010 INPUT "ENTER AMOUNT";AM 
1020 GOSUB 8000 
1025 AM$=STR$(AM)
1030 PRINT DA$;:PRINT CI$;:PRINT USING "$$#####.##";AM
1500 OPEN "D",#1,"IN.DAT",30
1510 FIELD #1,10 AS DD$,10 AS TT$,10 AS CA$
1550 LSET DD$=DA$
1560 LSET TT$=CI$
1570 LSET CA$=AM$
1575 PUT #1,1+Q
1580 Q=Q+1
1590 CLOSE #1
1600 PRINT " ":PRINT "PRESS ENTER TO CONTINUE";
1610 INPUT L 
1615 IT=IT+AM
1617 GT=GT +IT
1620 GOTO 140

2000 CLS
2010 INPUT "ENTER AMOUNT";IM 
2020 GOSUB 8000
2025 IM$=STR$(IM)
2030 PRINT IA$;:PRINT CI$;:PRINT USING "$$#####.##";IM
2500 OPEN "D",#1,"OUT.DAT",30
2510 FIELD #1,10 AS PD$,10 AS PT$,10 AS PA$
2550 LSET PD$=DA$
2560 LSET PT$=CI$
2570 LSET PA$=IM$
2575 PUT #1,1+K
2580 K=K+1
2590 CLOSE #1
2600 PRINT " ":PRINT "PRESS ENTER TO CONTINUE";
2610 INPUT L 
2615 DT=DT-(IM*-1)
2617 GT=GT +DT
2620 GOTO 140

3000 OPEN "D",#1,"IN.DAT",30:IT=0
3001 LO =31
3005 CLS:IF LOF(1) < 1 THEN PRINT "NO DATA TO SHOW":CLOSE #1: GOTO 140
3006 PRINT "#   date      from       amount"
3010 FIELD #1,10 AS DD$,10 AS TT$,10 AS CA$
3020 FOR E=1 TO LOF(1)
3030 GET #1,E 
3035 IT = IT + VAL(CA$)
3040 PRINT @ LO, E;
3041 PRINT @ LO+4, DD$;
3042 PRINT @ LO +15, TT$;
3043 PRINT @ LO +22, "";
3045 PRINT USING "$$#####.##";VAL(CA$)
3047 LO =LO +32
3050 NEXT E 
3055 PRINT USING "              total  $$#####.##";IT
3060 CLOSE #1
3070 PRINT " ":PRINT "PRESS 0 ENTER TO CONTINUE";
3080 INPUT L 
3090 IF L > 0 THEN 3100
3095 GOTO 140
3100 INPUT "AMOUNT";AM$
3110 GOSUB 8000
3120 OPEN "D",#1,"IN.DAT",30
3130 FIELD #1,10 AS DD$,10 AS TT$,10 AS CA$
3150 LSET DD$=DA$
3160 LSET TT$=CI$
3170 LSET CA$=AM$
3180 PUT #1,L
3190 CLOSE #1
3195 GOSUB 5000
3200 GOTO 3000

4000 OPEN "D",#1,"OUT.DAT",30:DT=0
4001 LO =31
4005 CLS:IF LOF(1) < 1 THEN PRINT "NO DATA TO SHOW":CLOSE #1: GOTO 140
4006 PRINT "#   date      from       amount"
4010 FIELD #1,10 AS PD$,10 AS PT$,10 AS PA$
4020 FOR E=1 TO LOF(1)
4030 GET #1,E 
4035 DT = DT + VAL(PA$)
4040 PRINT @ LO, E;
4042 PRINT @ LO+4, PD$;
4043 PRINT @ LO +15, PT$;
4044 PRINT @ LO +22, "";
4045 PRINT USING "$$#####.##";VAL(PA$)
4047 LO =LO +32
4050 NEXT E 
4055 PRINT USING "              total  $$#####.##";DT
4060 CLOSE #1
4070 PRINT " ":PRINT "PRESS 0 ENTER TO CONTINUE";
4080 INPUT L 
4090 IF L > 0 THEN 4100
4095 GOTO 140
4100 INPUT "AMOUNT";IM$
4110 GOSUB 8000
4120 OPEN "D",#1,"OUT.DAT",30
4130 FIELD #1,10 AS PD$,10 AS PT$,10 AS PA$
4150 LSET PD$=DA$
4160 LSET PT$=CI$
4170 LSET PA$=IM$
4180 PUT #1,L
4190 CLOSE #1
4195 GOSUB 6000
4200 GOTO 4000

5000 OPEN "D",#1,"IN.DAT",30:GT=0
5005 IF LOF(1) <1 THEN CLOSE #1:RETURN
5007 FIELD #1,10 AS DD$,10 AS TT$,10 AS CA$
5010 Q=LOF(1)
5020 FOR E=1 TO Q
5030 GET #1,E 
5040 IT=IT+VAL(CA$)
5050 NEXT E
5060 CLOSE #1
5070 RETURN

6000 OPEN "D",#1,"OUT.DAT",30:GT=0
6005 IF LOF(1) <1 THEN CLOSE #1:RETURN
6007 FIELD #1,10 AS DD$,10 AS TD$,10 AS CD$
6010 K=LOF(1)
6020 FOR E=1 TO K
6030 GET #1,E 
6040 DT=DT+VAL(CD$)
6050 NEXT E
6060 CLOSE #1
6070 RETURN

7000 CLS
7005 FOR X=1 TO 12:MI(X)=0:MD(X)=0:NEXT X
7100 OPEN "D",#1,"IN.DAT",30
7105 IF LOF(1) <1 THEN CLOSE #1:GOTO 7200
7107 FIELD #1,10 AS MD$,10 AS MC$,10 AS MZ$
7110 G=LOF(1)
7120 FOR E=1 TO G
7130 GET #1,E 
7140 MT= VAL(LEFT$(MD$,2))
7150 GOSUB 7500
7160 NEXT E
7170 CLOSE #1

7200 OPEN "D",#1,"OUT.DAT",30
7205 IF LOF(1) <1 THEN CLOSE #1:A$="R":RETURN
7207 FIELD #1,10 AS ID$,10 AS IC$,10 AS IZ$
7210 G=LOF(1)
7220 FOR E=1 TO G
7230 GET #1,E 
7240 IQ= VAL(LEFT$(ID$,2))
7250 GOSUB 7550
7260 NEXT E 
7270 CLOSE #1

7300 CLS:PRINT "MON#  INCOME     DEBT     TOTAL":LO=31
7305 FOR X= 1 TO 12
7310 PRINT @ LO,X;
7320 PRINT @ LO+3,"";:PRINT USING "$$#####.##";MI(X);:MU=MU+MI(X)
7330 PRINT @ LO+13,"";:PRINT USING "$$#####.##";MD(X);:MG=MG+MD(X)
7340 PRINT @ LO+23,"";:PRINT USING "$$####.##";MI(X)-MD(X):MP=MP+(MI(X)-MD(X))
7345 LO=LO+32
7350 NEXT X
7355 PRINT @ LO+1,"totals":LO=LO+32
7360 PRINT @ LO+2,"";:PRINT USING "$$#####.##";MU;
7370 PRINT @ LO+12,"";:PRINT USING "$$#####.##";MG;
7380 PRINT @ LO+22,"";:PRINT USING "$$#####.##";MP
7430 A$=INKEY$
7435 IF A$=" " THEN 140
7440 GOTO 7430

7500 FOR X = 1 TO 12
7510 IF X = MT THEN MI(MT)=MI(MT)+VAL(MZ$)
7530 NEXT X
7540 RETURN

7550 FOR X = 1 TO 12
7560 IF X = IQ THEN MD(IQ)=MD(IQ)+VAL(IZ$)
7570 NEXT X
7800 RETURN

8000 CLS:PRINT "       categories":PRINT""
8005 OPEN "D",#1,"CAT.DAT",10:QQ=0
8010 IF LOF(1) <1 THEN QQ=99:CLOSE#1:GOTO 8530
8020 FIELD #1,10 AS CT$
8030 G=LOF(1)
8040 FOR E=1 TO G
8050 GET #1,E 
8070 PRINT E;" ";CT$
8075 CG$(E)=CT$
8080 NEXT E
8085 CLOSE #1
8090 IF A=6 THEN INPUT "99 TO ADD OR # TO EDIT";QQ
8100 IF A=6 AND QQ=99 THEN 8530 
8105 IF A=6 AND QQ<1 THEN A$="R":RETURN
8106 IF A=6 THEN 8500
8107 INPUT L
8110 CI$=CG$(L):A$=""
8120 A$="R":RETURN

8500 PRINT "EDIT CATEGORIES"
8530 OPEN "D",#1,"CAT.DAT",10
8540 FIELD #1,10 AS CA$
8550 B=LOF(1)+1
8555 IF QQ=99 THEN B=LOF(1)+1:GOTO 8570
8560 IF QQ<99 AND QQ>0 THEN B=QQ
8570 INPUT "CAT NAME";CD$
8580 LSET CA$=CD$
8600 PUT #1,B
8610 CLOSE #1
8620 IF A<6 THEN GOTO 8000
8630 A$="R":RETURN

9000 CLS
9010 PRINT "SETTINGS":PRINT "" 
9020 PRINT "1- CLEAR INCOME DATA"
9030 PRINT "2- CLEAR EXPENSE DATA"
9040 PRINT "3- CLEAR CATEGORIES DATA"
9050 PRINT "4- MAIN MENU"
9055 A$=INKEY$
9060 IF A$="1" THEN PRINT "CLEARED INCOME DATA":KILL "IN/DAT":IT=0
9070 IF A$="2" THEN PRINT "CLEARED EXEPNSE DATA":KILL "OUT/DAT":DT=0
9080 IF A$="3" THEN PRINT "CLEARED CATEGORIES":KILL "CAT/DAT"
9090 IF A$ ="4" THEN GOTO 140
9100 GOTO 9055

10000 OPEN "D",#1,"DATE.DAT",8
10010 FIELD #1,8 AS DJ$
10020 B=LOF(1)
10030 LSET DJ$=DA$
10040 PUT #1,1
10050 YE$=RIGHT$(DJ$,2)
10060 CLOSE #1
10070 RETURN

10500 OPEN "D",#1,"DATE.DAT",8
10510 B=LOF(1)
10520 IF B<1 THEN CLOSE #1:RETURN
10530 FIELD #1,8 AS DJ$
10540 GET #1,1
10550 YE$=RIGHT$(DJ$,2)
10560 MO$=LEFT$(DJ$,2)
10565 DL$=MID$(DJ$,4,2)
10570 CLOSE #1
10580 RETURN

11000 OPEN "D",#1,"CAT.DAT",10:
11010 IF LOF(1) <1 THEN CLOSE#1: A$="R":RETURN
11020 FIELD #1,10 AS CT$
11030 G=LOF(1)
11040 FOR E=1 TO G
11050 GET #1,E 
11070 CG$(E)=CT$
11080 NEXT E
11090 CLOSE #1
11100 FOR X=1 TO G 
11110 OT(X)=0
11120 NEXT X
11130 IP=0

11300 OPEN "D",#1,"OUT.DAT",30
11310 IF LOF(1) <1 THEN CLOSE #1:A$="R":RETURN
11320 FIELD #1,10 AS ID$,10 AS IC$,10 AS IZ$
11330 P=LOF(1)
11340 FOR E=1 TO P
11350 GET #1,E 
11355 IL$(E)=IC$
11360 IP= VAL(IZ$)
11365 FOR W = 1 TO G
11370 IF IL$(E)=CG$(W) THEN OT(W)=OT(W)+IP
11375 NEXT W
11390 NEXT E 
11395 CLOSE #1
11400 CLS
11405 PRINT "CATGEGORY     AMOUNT"
11410 FOR X = 1 TO G
11420 PRINT CG$(X);
11430 PRINT USING "$$#####.##";OT(X)
11440 NEXT X
11450 INPUT L 
11500 A$="R":RETURN
12000 END